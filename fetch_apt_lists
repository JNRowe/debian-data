#! /bin/zsh

setopt err_exit no_unset warn_create_global

autoload -U zargs

BASE=https://deb.debian.org/
RELEASES=(bullseye bookworm trixie forky)

fetch() {
    local location=$1

    local url=$BASE$location
    local safename=${${url/#https:\/\/}//\//_}
    local suite=${${${(ps:/:)location}[3]}%-*}
    local filename=$suite/$safename
    local output=${filename/%.(bz2|xz)}
    local etagname=.etags/$safename
    local ref_time
    [[ -f $output ]] && ref_time=$output || ref_time="2004 Nov 13"

    mkdir -p logs/$suite

    () {
        echo "--- Fetching $url"
        curl --connect-timeout 5 --max-time 30 \
            --etag-compare $etagname \
            --etag-save $etagname \
            --time-cond $ref_time --output $filename $url
    } &> logs/$filename
}

process_Packages() {
    local filename=$1

    local _parts=(${(s:_:)filename})
    local suite=${_parts[4]/-/\/}
    local component=${_parts[5]}

    mkdir -p logs/$suite

    () {
        echo "--- Processing $filename"
        awk -f split_packages.awk -v output_dir=$suite $filename
        rm -f $filename
    } &> logs/$suite/process_$component
}

RELEASE_FILES=(
    debian/dists/RELEASE{,-updates}/InRelease
    debian/dists/RELEASE{,-updates}/{contrib,main,non-free{,-firmware}}/binary-amd64/Packages.xz
    debian/dists/RELEASE{,-updates}/{contrib,main,non-free{,-firmware}}/i18n/Translation-en.xz
    debian-security/dists/RELEASE-security/InRelease
    debian-security/dists/RELEASE-security/{contrib,main,non-free{,-firmware}}/binary-amd64/Packages.xz
    debian-security/dists/RELEASE-security/{contrib,main,non-free{,-firmware}}/i18n/Translation-en.xz
)

# Bullseye requires manual setup as a few files still use bzip2 compression.
LOCATIONS=(
    debian/dists/bullseye/InRelease
    debian/dists/bullseye/contrib/binary-i386/Packages.xz
    debian/dists/bullseye/contrib/i18n/Translation-en.bz2
    debian/dists/bullseye/main/binary-i386/Packages.xz
    debian/dists/bullseye/main/i18n/Translation-en.bz2
    debian/dists/bullseye/non-free/binary-i386/Packages.xz
    debian/dists/bullseye/non-free/i18n/Translation-en.bz2
    debian/dists/bullseye-updates/InRelease
    debian/dists/bullseye-updates/contrib/binary-i386/Packages.xz
    debian/dists/bullseye-updates/contrib/i18n/Translation-en.bz2
    debian/dists/bullseye-updates/main/binary-i386/Packages.xz
    debian/dists/bullseye-updates/main/i18n/Translation-en.bz2
    debian/dists/bullseye-updates/non-free/binary-i386/Packages.xz
    debian/dists/bullseye-updates/non-free/i18n/Translation-en.bz2
    debian-security/dists/bullseye-security/InRelease
    debian-security/dists/bullseye-security/contrib/binary-i386/Packages.xz
    debian-security/dists/bullseye-security/contrib/i18n/Translation-en.xz
    debian-security/dists/bullseye-security/main/binary-i386/Packages.xz
    debian-security/dists/bullseye-security/main/i18n/Translation-en.xz
    debian-security/dists/bullseye-security/non-free/binary-i386/Packages.xz
    debian-security/dists/bullseye-security/non-free/i18n/Translation-en.xz
)

for release (${RELEASES/bullseye}) {
    LOCATIONS+=(${RELEASE_FILES/RELEASE/$release})
}

mkdir --parents .etags $RELEASES

zargs --verbose --max-args 1 --max-procs $(nproc) -- $LOCATIONS -- fetch

zargs --max-args 3 --max-procs $(nproc) --no-run-if-empty -- \
    ${^RELEASES}/*.xz(N) -- \
    unxz --force --verbose
zargs --max-args 3 --max-procs $(nproc) --no-run-if-empty -- \
    bullseye/*.bz2(N) -- \
    bunzip2 --force --verbose

zargs --verbose --max-args 1 --max-procs $(nproc) --no-run-if-empty -- \
    ${^RELEASES}/*_Packages(N) -- \
    process_Packages

rm -f -- ${^RELEASES}/*(NL0)

more logs/*/*(.) | cat
more logs/*/*/* | cat
