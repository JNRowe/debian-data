#! /bin/zsh

setopt err_exit no_unset warn_create_global

autoload -U zargs

mkdir --parents bullseye
mkdir --parents .etags

base=https://deb.debian.org/

for location (
    debian/dists/bullseye/InRelease \
    debian/dists/bullseye/contrib/binary-i386/Packages.xz \
    debian/dists/bullseye/contrib/i18n/Translation-en.bz2 \
    debian/dists/bullseye/main/binary-i386/Packages.xz \
    debian/dists/bullseye/main/i18n/Translation-en.bz2 \
    debian/dists/bullseye/non-free/binary-i386/Packages.xz \
    debian/dists/bullseye/non-free/i18n/Translation-en.bz2 \
    debian/dists/bullseye-updates/InRelease \
    debian/dists/bullseye-updates/contrib/binary-i386/Packages.xz \
    debian/dists/bullseye-updates/contrib/i18n/Translation-en.bz2 \
    debian/dists/bullseye-updates/main/binary-i386/Packages.xz \
    debian/dists/bullseye-updates/main/i18n/Translation-en.bz2 \
    debian/dists/bullseye-updates/non-free/binary-i386/Packages.xz \
    debian/dists/bullseye-updates/non-free/i18n/Translation-en.bz2 \
    debian-security/dists/bullseye-security/InRelease \
    debian-security/dists/bullseye-security/contrib/binary-i386/Packages.xz \
    debian-security/dists/bullseye-security/contrib/i18n/Translation-en.xz \
    debian-security/dists/bullseye-security/main/binary-i386/Packages.xz \
    debian-security/dists/bullseye-security/main/i18n/Translation-en.xz \
    debian-security/dists/bullseye-security/non-free/binary-i386/Packages.xz \
    debian-security/dists/bullseye-security/non-free/i18n/Translation-en.xz
) {
    url=$base$location
    safename=${${url/#https:\/\/}//\//_}
    filename=bullseye/$safename
    output=${filename/%.(bz2|xz)}
    etagname=.etags/$safename
    [[ -f $output ]] && ref_time=$output || ref_time="2004 Nov 13"
    # More recent versions of curl don't fail on missing etag file
    [[ -f $etagname ]] && ref_etag=$etagname || ref_etag=/dev/null
    echo "--- $url"
    # Once a more recent version of curl is available in actions
    # --etag-{compare,save} can return to using the same file.
    curl --connect-timeout 5 --max-time 30 \
        --etag-compare $ref_etag \
        --etag-save $etagname.tmp \
        --verbose \
        --time-cond $ref_time --output $filename $url
    if [[ -s $etagname.tmp ]] {
        mv $etagname{.tmp,}
    } else {
        rm $etagname.tmp
    }
}

zargs --max-args 3 --max-procs $(nproc) --no-run-if-empty -- \
    bullseye/*.xz(N) -- \
    unxz --force --verbose
zargs --max-args 3 --max-procs $(nproc) --no-run-if-empty -- \
    bullseye/*.bz2(N) -- \
    bunzip2 --force --verbose

rm -f -- bullseye/*(NL0)
